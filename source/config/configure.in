dnl    $Id: configure.in,v 1.40 2000/01/16 17:31:43 oliver Exp $
dnl    Process this file with autoconf to produce a configure script.
dnl   

dnl
dnl    disable caching - just to be sure
dnl   
define([AC_CACHE_LOAD], )dnl
define([AC_CACHE_SAVE], )dnl

dnl 
dnl    some initialization stuff...
dnl 
AC_INIT(KERNEL/atom.C)
AC_CONFIG_HEADER([
	config.h:config/config.h.in
])
AC_LANG_CPLUSPLUS

dnl		
dnl    define command line arguments for QT, Mesa, and OpenGL
dnl    includes and libraries
dnl
AC_ARG_ENABLE(optimization,    [  --enable-optimization   optimize the library for speed. Omit debug info.!])
AC_ARG_ENABLE(debuginfo,       [  --disable-debuginfo     remove -g from the compiler flags (omit debug information)])
AC_ARG_ENABLE(BALLVIEW,        [  --disable-BALLVIEW      disable the compilation of BALLVIEW, the visualization component])
AC_ARG_ENABLE(64,              [  --enable-64             create 64 bit binaries (if allowed by the compiler)])
AC_ARG_WITH(compiler,          [  --with-compiler=CXX     use CXX as C++ compiler])
AC_ARG_WITH(cxxflags,          [  --with-cxxflags=FLAGS   add FLAGS to the C++ compiler flags])
AC_ARG_WITH(ldflags,           [  --with-ldflags=FLAGS    add FLAGS to the linker flags])
AC_ARG_WITH(qt-incl,           [  --with-qt-incl=DIR      QT header files are in DIR])
AC_ARG_WITH(qt-libs,           [  --with-qt-libs=DIR      QT libraries are in DIR])
AC_ARG_WITH(opengl-incl,       [  --with-opengl-incl=DIR  OpenGL/Mesa header files are in DIR/GL])
AC_ARG_WITH(opengl-libs,       [  --with-opengl-libs=DIR  OpenGL/Mesa libraries are in DIR/GL])
AC_ARG_WITH(mesa,              [  --with-mesa             use MESA instead of OpenGL])
AC_ARG_WITH(libxnet,           [  --without-libxnet       use -lsocket -lnsl rather than -lxnet (under Solaris)])

dnl
dnl    interpret switches...
dnl
if test "$enable_optimization" = "yes" ; then
	DEBUG=false
else
	DEBUG=true
fi

if test "$enable_debuginfo" = "no" ; then
	DEBUG_INFO=false
else
	DEBUG_INFO=true
fi

if test "$enable_64" = "yes" ; then
	BINFMT_64_BIT=true
else
	BINFMT_64_BIT=false
fi

if test "$with_compiler" != "" ; then
	CXX=$with_compiler
fi

if test "$with_cxxflags" != "" ; then
	CPPFLAGS=$with_cxxflags
fi

if test "$with_ldflags" != "" ; then
	LDFLAGS=$with_ldflags
fi

if test "$with_qt_incl" != "" ; then
	QT_INCPATH=$with_qt_incl
fi
if test "$with_qt_libs" != "" ; then
	QT_LIBPATH=$with_qt_libs
fi

if test "$with_opengl_incl" != "" ; then
	OPENGL_INCPATH=$with_opengl_incl
fi
if test "$with_qt_libs" != "" ; then
	OPENGL_LIBPATH=$with_opengl_libs
fi

if test "$with_mesa" != "yes" ; then	
	BALLVIEW_PLATFORM=OpenGL
else
 	BALLVIEW_PLATFORM=Mesa
fi

if test "$with_libxnet" != "no" ; then	
	USE_LIBXNET=true
else
	USE_LIBXNET=false
fi

if test "$with_BALLVIEW" = "no" ; then
	USE_BALLVIEW=false
else	
	USE_BALLVIEW=true
fi

dnl
dnl 	global definitions
dnl

dnl		the file containing the list of supported (configure`d) 
dnl   binary formats. configure will add a line with ${BINFMT} to this file
BINFORMAT_FILE=config/binary_formats


dnl		define a macro to remove the directory name
dnl		from a fully specified path
dnl
AC_DEFUN(AC_BASENAME,[
	TMP__DIR="$1/"
	while test "${TMP__DIR}" != "" ; do
		TMP__NAME=`echo ${TMP__DIR}|cut -d/ -f1`
		TMP__DIR=`echo ${TMP__DIR}|cut -d/ -f2-`
		if test "${TMP__DIR}" = "${TMP__NAME}" ; then
			TMP__DIR=""
		fi
	done
])
	

dnl    define a macro to inform the user about failed tests for programs
dnl    it checks for the unix command given as second parameter and
dnl    sets the shell variable given as second parameter to its absolute path
dnl 
AC_DEFUN(AC_MSG_PATH_PROG,[
	AC_PATH_PROG($1,$2,no)
	if test $$1 = no ; then
		AC_MSG_RESULT()
		AC_MSG_RESULT([This script requires the unix command $2, but cannot find it.])
		AC_MSG_RESULT([Please add the correct path to $2 to your \$PATH variable])
		AC_MSG_RESULT([and restart configure.])
		AC_MSG_RESULT()
		AC_MSG_ERROR(aborted)
		exit
	fi
])

dnl
dnl    define macro to search for header files that may be somewhere in the filesystem
dnl    if ${FIND}!=- (i.e. it has been set BEFORE py AC_PATH_PROG!) find will be used
dnl    too if everything fails - this may take some time...
dnl
dnl    syntax: AC_FIND_HEADER(<PATH_VAR>,<header.h>,<additional dirnames>)
dnl    
dnl        PATH_VAR will be set to the include path or to empty string (if not found)
dnl        header.h is the header file name (e.g. wait.h, GL/gl.h)
dnl        additional dirnames are included in searches these should be absolute names!
dnl 

AC_DEFUN(AC_FIND_HEADER,[
	_INCLUDES=

	dnl    immediate return on predefined directory (read from file?)
	if test "${$1}" != "" ; then
		_INCLUDES=${$1}
	fi

	if test "${_INCLUDES}" = "" ; then
		for i in /usr/include /opt/include $3 ; do
			if test -f "$i/$2" && test "${_INCLUDES}" = ""; then
				_INCLUDES="$i"
			fi
		done
	fi

	if test "${_INCLUDES}" = "" ; then
		for i in /usr/*/include /opt/*/include ; do
			if test -f "$i/$2" && test "${_INCLUDES}" = ""; then
				_INCLUDES="$i"
			fi
		done
	fi
	
	if test "${_INCLUDES}" = "" ; then
		for i in /opt/*/*/include /usr/*/*/include /usr/local/*/*/include ; do
			if test -f "$i/$2" && test "${_INCLUDES}" = ""; then
				_INCLUDES="$i"
			fi
		done
	fi

	if test "${_INCLUDES}" = "" && test "${FIND}" != "-" ; then
		if test "${FIND_KNOWS_PATH}" = false ; then
			FIND_OPT="-name"
			_TMP_FIND_NAME="$2"
			while test _`egrep / $_TMP_FIND_NAME`_ != __ ; do
				_TMP_FIND_NAME=`echo ${_TMP_FIND_NAME}|${CUT} -d/ -f2-`
			done
			
			FIND_ARG="\*${_TMP_FIND_NAME}\*"
		else
			FIND_OPT="-path"
			FIND_ARG="\*$2\*"
		fi

		_TMP=`${FIND} /usr ${FIND_OPT} ${FIND_ARG} -print 2>/dev/null`
		for j in ${_TMP} ; do
			if test "${_INCLUDES}" = "" ; then
				_INCLUDES=`echo $j|${SED} "s/include\/.\*\$/include/"`
			fi
		done
			_
		
		if test "${_INCLUDES}" = "" ; then
			_TMP=`${FIND} /opt ${FIND_OPT} ${FIND_ARG} -print 2>/dev/null`
			for j in ${_TMP} ; do
				if test "${_INCLUDES}" = "" ; then
					_INCLUDES=`echo $j|${SED} "s/include\/.\*\$/include/"`
				fi
			done
		fi

		if test "${_INCLUDES}" = "" && test "$3" != ""; then
			for i in $3 /dev/null ; do
				_TMP=`${FIND} $i ${FIND_OPT} ${FIND_ARG} -print 2>/dev/null`
				for j in ${_TMP} ; do
					if test "${_INCLUDES}" = "" ; then
						_INCLUDES=`echo $j|${SED} "s/include\/.\*\$/include/"`
					fi
				done
			done
		fi
	fi

	$1="${_INCLUDES}"
])


dnl
dnl    define macro to search for libraries that may be somewhere in the filesystem
dnl    if ${FIND}!=- (i.e. it has been set BEFORE py AC_PATH_PROG!) find will be used
dnl    too if everything fails - this may take some time...
dnl
dnl    syntax: AC_FIND_LIB(<PATH_VAR>,<libXXX>,<additional dirnames>)
dnl    
dnl        PATH_VAR will be set to the library path or to empty string (if not found)
dnl        libXXX is the header file name (e.g. libGLUT, libGL) .a, .so etc. should be omitted
dnl        additional dirnames ar included in searches these should be absolute names!
dnl 

AC_DEFUN(AC_FIND_LIB,[
	_LIBS=

	dnl   immediate "return" on preset directory (read from file?)
	if test "${$1}" != "" ; then
		_LIBS=${$1}
	fi

	if test "${_LIBS}" = "" ; then
		for i in /usr/lib /opt/lib $3 ; do
			for j in $i/$2.* ; do
				if test -f "$j" && test "${_LIBS}" = ""; then
					_LIBS="$i"
				fi
			done
		done
	fi

	if test "${_LIBS}" = "" ; then
		for i in /usr/*/lib /opt/*/lib ; do
			for j in $i/$2.* ; do
				if test -f "$j" && test "${_LIBS}" = ""; then
					_LIBS="$i"
				fi
			done
		done
	fi
	
	if test "${_LIBS}" = "" ; then
		for i in /opt/*/*/lib /usr/*/*/lib /usr/local/*/*/lib ; do
			for j in $i/$2.* ; do
				if test -f "$j" && test "${_LIBS}" = ""; then
					_LIBS="$i"
				fi
			done
		done
	fi

	if test "${_LIBS}" = "" && test "${FIND}" != "-" ; then
		if test "${_LIBS}" = "" && test "$3" != ""; then
			for i in $3 /dev/null; do
				if test "${_LIBS}" = "" ; then
					_TMP=`${FIND} $i -name "$2*" -print 2>/dev/null`
					for j in ${_TMP} ; do
						if test "${_LIBS}" = "" ; then
							_LIBS=`echo $j|${SED} "s/\/$2.*/\//"`
						fi
					done
				fi
			done
		fi
		
		if test "${_LIBS}" = "" ; then
			_TMP=`${FIND} /opt -name "$2*" -print 2>/dev/null`
			for j in ${_TMP} ; do
				if test "${_LIBS}" = "" ; then
					_LIBS=`echo $j|${SED} "s/\/$2.*/\//"`
				fi
			done
		fi

		if test "${_LIBS}" = "" ; then
			_TMP=`${FIND} /usr -name "$2*" -print 2>/dev/null`
			for j in ${_TMP} ; do
				if test "${_LIBS}" = "" ; then
					_LIBS=`echo $j|${SED} "s/\/$2.*/\//"`
				fi
			done
		fi
		
	fi

	$1="${_LIBS}"
])
	


dnl
dnl    just trying to find myself
dnl

BALL_PATH=`cd ..; pwd`


dnl
dnl   check for programs used to determine architecture
dnl

AC_MSG_PATH_PROG(UNAME,uname)
AC_MSG_PATH_PROG(CUT,cut)
AC_MSG_PATH_PROG(TR,tr)
AC_MSG_PATH_PROG(AR,ar)

dnl
dnl    determine OS and architecture and all this stuff
dnl

AC_SUBST(OSMAJOR)
AC_SUBST(OS)
AC_SUBST(OSREV)
AC_SUBST(BINFMT)
AC_SUBST(BINFMT_PATH)
AC_SUBST(ARCHITECTURE)

AC_MSG_CHECKING(your OS)
OS=`${UNAME} -s`
OSREV=`${UNAME} -r`
OSMAJOR=`echo $OSREV|${CUT} -d"." -f1`

dnl		default...
BINFMT="${OS}"

if test "$OS" = SunOS ; then
	if test "$OSMAJOR" = 5 ; then
		OS=Solaris
		ARCHITECTURE=`${UNAME} -p`
		BINFMT="${OS}-${ARCHITECTURE}"
	else
		OS=SunOS
	fi
fi

if test "$OS" = Linux ; then
	PROCESSOR=`${UNAME} -m`
	ARCHITECTURE=unknown
	if test "${PROCESSOR}" = sparc -o "${PROCESSOR}" = SPARC ; then
		ARCHITECTURE=sparc
		BINFMT=Linux-sparc
	fi
	if test `echo $PROCESSOR|${CUT} -c1` = i ; then
		ARCHITECTURE=i386
		BINFMT=Linux-i386
	fi
	if test `echo $PROCESSOR` = alpha ; then
		ARCHITECTURE=alpha
		BINFMT=Linux-alpha
	fi

	if test "${ARCHITECTURE}" = "unknown" ; then
		AC_MSG_RESULT(OS: ${OS} / hardware: ${PROCESSOR})
		AC_MSG_RESULT(Sorry - this architecture is currently not supported...)
		AC_MSG_ERROR(aborted)
	fi
fi

if test ${OS} = IRIX64 ; then
	OS=IRIX
fi

if test $OS = IRIX ; then
	if test "$OSMAJOR" = 5 ; then
		BINFMT=IRIX5
	fi
	if test "$OSMAJOR" = 6 ; then
		BINFMT=IRIX6
	fi
fi

if test "$OS" != Linux && test "$OS" != Solaris && test "$OS" != IRIX ; then
	AC_MSG_RESULT(Sorry - your OS is currently not supported...)
	AC_MSG_ERROR(aborted)
fi

dnl
dnl 	create OS defines in config.h:
dnl
if test "$OS" = Linux ; then
	AC_DEFINE(BALL_OS_LINUX,LINUX)
fi
if test "$OS" = Solaris ; then
	AC_DEFINE(BALL_OS_SOLARIS,SOLARIS)
fi
if test "$OS" = IRIX ; then
	AC_DEFINE(BALL_OS_IRIX,IRIX)
fi

dnl
dnl		create ARCHITECTURE defines
dnl
if test "$ARCHITECTURE" = sparc ; then
	AC_DEFINE(BALL_ARCH_SPARC,SPARC)
fi
if test "$ARCHITECTURE" = i386 ; then
	AC_DEFINE(BALL_ARCH_I386,I386)
fi
if test "$ARCHITECTURE" = mips ; then
	AC_DEFINE(BALL_ARCH_MIPS,MIPS)
fi
if test "$ARCHITECTURE" = alpha ; then
	AC_DEFINE(BALL_ARCH_ALPHA,ALPHA)
fi

AC_MSG_RESULT($OS $OSREV (BINFMT=$BINFMT))

dnl
dnl	some definitions the depend solely on the OS
dnl
SHARED_LIB_SUFFIX=so
if test "${OS}" = HP-UX ; then
	SHARED_LIB_SUFFIX=sl
fi
AC_SUBST(SHARED_LIB_SUFFIX)

dnl
dnl    check for programs needed for build
dnl 

AC_PROG_LN_S
AC_MSG_PATH_PROG(CP,cp,no)
AC_MSG_PATH_PROG(RM,rm,no)
AC_MSG_PATH_PROG(MV,mv,no)
AC_MSG_PATH_PROG(LN,ln,no)
AC_MSG_PATH_PROG(SED,sed,no)
AC_MSG_PATH_PROG(FIND,find,no)
AC_MSG_PATH_PROG(DIFF,diff,no)
AC_MSG_PATH_PROG(TAIL,tail,no)
AC_MSG_PATH_PROG(GREP,grep,no)
AC_MSG_PATH_PROG(CAT,cat,no)

dnl   
dnl   check whether find can be called with the parameter -path
dnl   (needed to find headers in a certain path like GL/libgl.h
dnl
if test "${FIND}" != "no" ; then
	RESULT=`${FIND} KERNEL -path . -print 2>&1`
	if test "${RESULT}" != "" ; then     dnl    did get an error message ... bad.
		FIND_KNOWS_PATH=false
	else
		FIND_KNOWS_PATH=true
	fi
fi
		

dnl		
dnl		Declare compiler search order
dnl			1) look for compiler defined in configure
dnl			2) look for vendor supplied compilers (CC)
dnl			3) check for g++, egcs, eg++, gcc
dnl		Except for Solaris, where the vendor supplied compiler
dnl		CC (at least releases 5.0 and below) is not usable.
if test "${OS}" = "Solaris" ; then
	CXX_SEARCH_ORDER="egcs g++ eg++ CC "
else 
	CXX_SEARCH_ORDER="CC egcs g++ eg++ "
fi
CXX_NAME=""



dnl		
dnl		If we are running IRIX, search for the older compilers, too
dnl
if test "${OS}" = "IRIX" ; then
	CXX_SEARCH_ORDER="${CXX_SEARCH_ORDER} NCC DCC OCC"
	CC_SEARCH_ORDER="${CC_SEARCH_ORDER} ncc dcc occ"
fi


dnl
dnl		Search for the C++ compiler
dnl

AC_MSG_RESULT(searching for a C++ compiler:)
if test "${CXX}" != "" ; then
	if test -f "${CXX}" ; then
		AC_MSG_RESULT(predefined in configure: ${CXX})
	else
		AC_PATH_PROG(CXXPATH,${CXX},no)
		if test "${CXXPATH}" = no ; then
			AC_MSG_RESULT()
			AC_MSG_RESULT(Cannot find ${CXX}. Please add it to your PATH)
			AC_MSG_RESULT(or specify an absolute path in configure.)
			AC_MSG_ERROR(aborted)
		else
			CXX=${CXXPATH}
		fi
	fi
else
	CXXPATH=""
	while test "${CXXPATH}" = "" ; do
		CXX=`echo ${CXX_SEARCH_ORDER}|${CUT} -d\  -f1`
		if test _`echo ${CXX} | ${TR} -d " "`_ = __ ; then
			CXXPATH="END"
		fi
		if test "${CXXPATH}" != "END" ; then
			AC_PATH_PROG(CXXPATH,${CXX},no)
			if test "${CXXPATH}" = no ; then
				CXXPATH=""
				unset ac_cv_path_CXXPATH
			else
				CXX=${CXXPATH}
			fi
		fi
			
		CXX_SEARCH_ORDER=`echo "${CXX_SEARCH_ORDER} " |${CUT} -d\  -f2-`
	done

	if test "${CXXPATH}" = "end" ; then
		AC_MSG_RESULT()
		AC_MSG_RESULT(Could not find a C++ compiler. Please change the settings)
		AC_MSG_RESULT(of your PATH environment variable (using setenv export))
		AC_MSG_RESULT(or specify an absolute path in configure by setting the variable)
		AC_MSG_RESULT(CXX=<pathname> or specify the compiler by passing the option)
		AC_MSG_RESULT(--with-compiler=<compiler> to configure.)
		AC_MSG_RESULT()
		AC_MSG_ERROR(aborted)
	fi
fi

dnl
dnl		extract the executable name of the compiler 
dnl 	as default compiler name (CXX_NAME is needed
dnl		to name the default directory the libraries 
dnl		reside in)
dnl

if test "${CXX_PATH}" = "" ; then
	if test "${CXX}" = "" ; then
		CXX_NAME=unknown
	else
		CXX_NAME="${CXX}"
	fi
else
	CXX_NAME="${CXX_PATH}"
fi
	
while test "`echo ${CXX_NAME}|grep /`" != "" ; do
	CXX_NAME=`echo ${CXX_NAME} | cut -d/ -f2-`
done


dnl
dnl		Check whether the C++ compiler is a GNU compiler
dnl

AC_MSG_CHECKING(for GNU compiler)
cat > /tmp/$$.conftest.c << EOF
#ifdef __GNUC__
GXX:true
#else
GXX:false
#endif
EOF

IS_GXX=`${CXX} -E /tmp/$$.conftest.c |egrep GXX|${CUT} -d: -f2|${TR} -d " "`
if test "${IS_GXX}" = "true" ; then
	AC_MSG_RESULT(yes)
	HAS_GPLUSPLUS=true
	CXX_NAME="g++"
else
	AC_MSG_RESULT(no)
	HAS_GPLUSPLUS=false
fi
rm /tmp/$$.conftest.c

dnl
dnl		Check for KAI C++ (if no GNU compiler found!)
dnl		At least under linux the damned frontend won't tell 
dnl		its version number, so we try to extract the word kai 
dnl		from its drivers options when called in verbose mode.
dnl		Nasty - but seems to work. Anybody with a better solution 
dnl 	should feel free to inform me!
dnl
if test "${IS_GXX}" = "false" ; then
	AC_MSG_CHECKING(for KAI C++ compiler)
	KAI=`${CXX} -v --version 2>&1 | sed "s/.*KAI.*/__KAI__/g" |sed "s/.*kai.*/__KAI__/g" | egrep "^__KAI__$" | sed -n 1p`
	if test "${KAI}" = "__KAI__" ; then
		IS_KCC=true
		AC_MSG_RESULT(yes)
		CXX_NAME="KCC"
	else
		IS_KCC=false
		AC_MSG_RESULT(no)
	fi
fi


dnl
dnl 	Try to find out the exact compiler release on different
dnl		operating systems and differntiate between gcc/egcs
dnl
if test "${IS_GXX}" = "true" ; then
	AC_MSG_CHECKING(compiler version)
	CXX_VERSION=`${CXX} --version`
	if test `echo ${CXX_VERSION}|${CUT} -c1-4` = "egcs" ; then
		IS_EGXX=true
		CXX_NAME="egcs"
		CXX_VERSION=`${CXX} -v 2>&1 | grep release | cut -d\( -f2 | cut -d\) -f1 | sed "s/egcs-//" | cut -d" " -f1`
		VERSION_OUTPUT="egcs ${CXX_VERSION}"
		CXX_COMPILER_NAME="egcs"
	else
		IS_EGXX=false
		VERSION_OUTPUT="g++ ${CXX_VERSION}"
		CXX_COMPILER_NAME="g++"
	fi
	
	AC_MSG_RESULT(${VERSION_OUTPUT})

	CXX_VERSION_1=`echo ${CXX_VERSION} | ${CUT} -d. -f1`
	CXX_VERSION_LENGTH=`echo ${CXX_VERSION} | sed "s/[^.]//g" | wc -c`
	if test "${CXX_VERSION_LENGTH}" -ge 2 ; then
		CXX_VERSION_2=`echo ${CXX_VERSION} | ${CUT} -d. -f2`
	fi
	if test "${CXX_VERSION_LENGTH}" -ge 3 ; then
		CXX_VERSION_3=`echo ${CXX_VERSION} | ${CUT} -d. -f3`
	fi
	if test "${CXX_VERSION_LENGTH}" -ge 4 ; then
		CXX_VERSION_4=`echo ${CXX_VERSION} | ${CUT} -d. -f4`
	fi

	if test "${IS_EGXX}" = "true" ; then	
		AC_MSG_RESULT()
		AC_MSG_RESULT(egcs won't compile BALL due to several internal compiler errors.)
		AC_MSG_RESULT(Please upgrade to a more recent version of GNU g++ (e.g. 2.95.2).)
		AC_MSG_RESULT(If you have alreadyinstalled a different compiler, you may)
		AC_MSG_RESULT(specify the path to this compiler by passing the option)
		AC_MSG_RESULT( --with-compiler=<compiler> to configure.)
		AC_MSG_RESULT()
		AC_MSG_ERROR(aborted)
	fi
	if test "${CXX_VERSION_1}" -lt 2 \
		-o "${CXX_VERSION_1}" = 2 -a "${CXX_VERSION_2}" -lt 95 ; then
		AC_MSG_RESULT()
		AC_MSG_RESULT([The version of gcc you are using is not supported by BALL.])
		AC_MSG_RESULT([Please update to a newer version of g++ (at least 2.95.x)])
		AC_MSG_RESULT([which can be obtained from])
		AC_MSG_RESULT([  ftp://gcc.gnu.org/pub/gcc/releases/index.html])
		AC_MSG_RESULT([or specify a different compiler using the option --with-compiler=])
		AC_MSG_RESULT()
		AC_MSG_ERROR(aborted)
	fi
else
	dnl
	dnl		Now we got a problem: we have to identify the compiler
	dnl		This is nearly impossible, but we give our best...
	dnl
	if test "${OS}" = "IRIX" ; then
		AC_MSG_CHECKING(compiler version)
		CXX_VERSION_STRING=`${CXX} -n32 -version 2>&1 | egrep ersion`
		if test "${CXX_VERSION_STRING}" = "" ; then
			AC_BASENAME(${CXX})
			CXX_VERSION="${TMP__NAME}"
			CXX_VERSION_OUTPUT="${CXX_VERSION} (unknown version)"
		else
			CXX_VERSION=`echo ${CXX_VERSION_STRING} | ${SED} "s/^.*ersion //g"`
			CXX_COMPILER_NAME=`echo ${CXX_VERSION_STRING} | ${CUT} -d\  -f1`
			CXX_VERSION_OUTPUT="${CXX_VERSION} (${CXX_COMPILER_NAME})"
		fi

		CXX_VERSION_1=`echo ${CXX_VERSION} | ${CUT} -d. -f1`
		CXX_VERSION_LENGTH=`echo ${CXX_VERSION} | sed "s/[^.]//g" | wc -c`
		if test "${CXX_VERSION_LENGTH}" -ge 2 ; then
			CXX_VERSION_2=`echo ${CXX_VERSION} | ${CUT} -d. -f2`
		fi
		if test "${CXX_VERSION_LENGTH}" -ge 3 ; then
			CXX_VERSION_3=`echo ${CXX_VERSION} | ${CUT} -d. -f3`
		fi
		if test "${CXX_VERSION_LENGTH}" -ge 4 ; then
			CXX_VERSION_4=`echo ${CXX_VERSION} | ${CUT} -d. -f4`
		fi

		AC_MSG_RESULT(${CXX_VERSION_OUTPUT})
	fi

	if test "${OS}" = "Solaris" ; then
		AC_MSG_CHECKING(compiler version)

		CXX_VERSION_STRING=`${CXX} -V 2>&1 | egrep CC:`

		CXX_VERSION=`echo ${CXX_VERSION_STRING} | ${TR} -d "abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ:" | ${TR} -s " " | ${CUT} -d\  -f2`
		changequote(<<,>>)
		CXX_VERSION_TEST=`echo ${CXX_VERSION} | ${SED} s/\^\\[0-9\\.\\]*[a-zA-Z\\.]*//g`
		changequote([,])
		if test "${CXX_VERSION_TEST}" != "" ; then
			AC_BASENAME(${CXX})
			CXX_VERSION="${TMP__NAME}"
			CXX_VERSION_OUTPUT="${CXX_VERSION} (unknown version)"
		else
			CXX_VERSION_1=`echo ${CXX_VERSION} | ${CUT} -d. -f1`
			CXX_VERSION_LENGTH=`echo ${CXX_VERSION} | sed "s/[^.]//g" | wc -c`
			if test "${CXX_VERSION_LENGTH}" -ge 2 ; then
				CXX_VERSION_2=`echo ${CXX_VERSION} | ${CUT} -d. -f2`
			fi
			if test "${CXX_VERSION_LENGTH}" -ge 3 ; then
				CXX_VERSION_3=`echo ${CXX_VERSION} | ${CUT} -d. -f3`
			fi
			if test "${CXX_VERSION_LENGTH}" -ge 4 ; then
				CXX_VERSION_4=`echo ${CXX_VERSION} | ${CUT} -d. -f4`
			fi
			CXX_VERSION_OUTPUT="${CXX_VERSION}"
		fi
		AC_MSG_RESULT(${CXX_VERSION_OUTPUT})

		if test "${CXX_VERSION_1}" -lt 6 ; then
			AC_MSG_RESULT()
			AC_MSG_RESULT(Wrong version of CC! CC before release 6 is not ANSI C++ compliant)
			AC_MSG_RESULT(Please upgrade to a more recent version of CC.)
			AC_MSG_ERROR(aborted)
		fi
	fi
fi

dnl
dnl		Assemble the complete compiler name by adding
dnl		the release numbers (if known) of the compiler
dnl

AC_MSG_CHECKING(standardized compiler name)
if test "${CXX_VERSION_1}" != "" ; then
	CXX_NAME="${CXX_NAME}_${CXX_VERSION_1}"
	if test "${CXX_VERSION_2}" != "" ; then
		CXX_NAME="${CXX_NAME}.${CXX_VERSION_2}"
		if test "${CXX_VERSION_3}" != "" ; then
			CXX_NAME="${CXX_NAME}.${CXX_VERSION_3}"
			if test "${CXX_VERSION_4}" != "" ; then
				CXX_NAME="${CXX_NAME}.${CXX_VERSION_4}"
			fi
		fi
	fi
fi

AC_MSG_RESULT(${CXX_NAME})


dnl
dnl		default makedepend name
dnl
CXX_MAKEDEPEND="makedepend"

dnl
dnl   run C++ dependencies first to file .Dependencies....
dnl
MAKEDEP_CXX_OPTS="-f.Dependencies -D__cplusplus --"

dnl normally makedepend doesn't need a redirection.
dnl if the C++-compiler creates the dependencies, the
dnl redirection to .Dependencies should be given in 
dnl MAKEDEP_CXX
MAKEDEP_CXX_SUFFIX=

dnl
dnl	 TEMPLATE_DIR contains the name of the template
dnl  database dir. This is required for a "make clean"
dnl
TEMPLATE_DIR=

dnl
dnl  Here go the g++-specific flags
dnl 
if test "${HAS_GPLUSPLUS}" = "true" ; then
	CXX_MAKEDEPEND="${CXX}"
	MAKEDEP_CXX_OPTS="-M"
	if test "${ARCHITECTURE}" = "alpha" ; then
		CPPFLAGS="${CPPFLAGS}"
	fi
	CPPFLAGS_D="${CPPFLAGS_D} -Wall"
	CPPFLAGS_DI="${CPPFLAGS_DI} -g"
	CPPFLAGS_O="${CPPFLAGS_O} -O2"
	MAKEDEP_CXX_SUFFIX=" >.Dependencies"
	CPPFLAGS="${CPPFLAGS} -fPIC"

	DYNAR="${CXX}"
	if test "${OS}" != "Solaris" ; then
		DYNAROPTS="-shared -fPIC -o"		
	else 
		DYNAROPTS="-G -fPIC -o"
	fi

	if test "${IS_EGXX}" = true; then
		BALL_TYPENAME=typename
	else
		if test "${CXX_VERSION_1}" -gt 2 -o "${CXX_VERSION_1}" -eq 2 -a "${CXX_VERSION_2}" -ge 8 ; then
			BALL_TYPENAME=typename
		fi
	fi
else
	dnl
	dnl		KAI C++ specific options
	dnl
	if test "${IS_KCC}" = true ; then
		dnl		KAI C++ stores a list of instantiated templates
		dnl		in directories called ti_files
		dnl		make clean should remove these
		TEMPLATE_DIR="ti_files"
		AR="${CXX}"
		DYNAR="${CXX}"
		AROPTS="-o"
		DYNAROPTS="-o"
		CXX_MAKEDEPEND="${CXX}"
		MAKEDEP_CXX_OPTS="-M"
		MAKEDEP_CXX_SUFFIX=" >.Dependencies"

		dnl	
		dnl		Someone at KAI seems to have the need
		dnl		to torture developpers by introducing 
		dnl		a new flag for position independent code 
		dnl		on EVERY platform...
		dnl		
		if test "${OS}" = Linux ; then
			CPPFLAGS="${CPPFLAGS} -fPIC"
		fi
		if test "${OS}" = Solaris ; then
			CPPFLAGS="${CPPFLAGS} -KPIC"
		fi
		if test "${OS}" = IRIX ; then
			CPPFLAGS="${CPPFLAGS} --pic"
		fi
		
		dnl		optimze as on highest level: this compiler 
		dnl		does a good job optimizing!
		CPPFLAGS_O="${CPPFLAGS_O} -O3 +K3"
		
		dnl		avoid high level optimization to
		dnl		get debuggable code...
		CPPFLAGS_D="${CPPFLAGS_D} +K0"
		CPPFLAGS_DI="${CPPFLAGS_DI} -g"
	else
		if test "${OS}" = IRIX ; then
			dnl  set the name for the template repository
			dnl
			TEMPLATE_DIR="ii_files"

			dnl	 set the default binary format (if none selected)
			dnl
			if test "${BINFMT_64_BIT}" = true ; then
				IRIX_BINFMT=64
				CXX_NAME="${CXX_NAME}_64"
			else
				IRIX_BINFMT=N32
				CXX_NAME="${CXX_NAME}_N32"
			fi
			
			BALL_TYPENAME=typename

			dnl 
			dnl			a version above 7.2 is required
			dnl
			if test "${CXX_VERSION_1}" -lt 7\
							-o "${CXX_VERSION_1}" -eq 7 -a "${CXX_VERSION_2}" = 10\
							-o "${CXX_VERSION_1}" -eq 7 -a "${CXX_VERSION_2}" = 20\
							-o "${CXX_VERSION_1}" -eq 7 -a "${CXX_VERSION_2}" -lt 2; then
				AC_MSG_RESULT()
				AC_MSG_RESULT(MipsPro CC version 7.30 or above is required. Please update your compiler.)
				AC_ERROR(Aborted)
			fi


			AR=${CXX}
			AROPTS="-ar -o"
			DYNAR=${CXX}
			DYNAROPTS="-LANG:std -shared -quickstart_info -no_unresolved -o"

			dnl  issue a warning about an old compiler with a broken ostream implementation: reopening a fstream
			dnl  and writing to it will omit the first 16k written to the stream. Nasty, but confirmed with SGI
			dnl  and fixed in 7.3.1.1m
			if test "${CXX_VERSION_1}" = 7 -a "${CXX_VERSION_2}" = 3 \
							-a "${CXX_VERSION_3}" = 1m -a "${CXX_VERSION_4}" = ""; then
				
				COMMENTS="${COMMENTS}\nPlease take care - this version of SGI CC (7.3.1m) contains serious bugs\n"
				COMMENTS="${COMMENTS}in its implementaion of fstream/iostream. This may lead to strange behaviour\n"
				COMMENTS="${COMMENTS}and causes PDBFile_test to fail. Please update your compiler.\n\n"
				AC_MSG_RESULT(${COMMENTS})
				ADDITIONAL_COMMENTS="${ADDITIONAL_COMMENTS}${COMMENTS}"
				COMMENTS=""
			fi

			CXX_MAKEDEPEND="${CXX}"
			MAKEDEP_CXX_OPTS="-M 2>/dev/null"
			MAKEDEP_CXX_SUFFIX=" >.Dependencies"

			if test "${IRIX_BINFMT}" = 64 ; then
				DEF_BOOL=false
				CPPFLAGS="$CPPFLAGS -64 -LANG:std"
				DYNAROPTS="-64 ${DYNAROPTS}"
				CPPFLAGS_O="${CPPFLAGS_O} -O2 -OPT:Olimit_opt=on -multigot -G 5"
				CPPFLAGS_D="${CPPFLAGS_D} -fullwarn -multigot -G 5 -DEBUG:woff=1375,3201,1424,1110,1209"
				CPPFLAGS_DI="${CPPFLAGS_DI} -g"
				LDFLAGS="$LDFLAGS -64 -LANG:std"
				AC_DEFINE(IRIX64,)
				ADDITIONAL_COMMENTS="${ADDITIONAL_COMMENTS}Please remember to use LD_LIBRARY64_PATH instead of\n LD_LIBRARY_PATH - you are using 64 bit objects!"
			fi
			if test "${IRIX_BINFMT}" = N32 ; then
				DEF_BOOL=false
				CPPFLAGS="$CPPFLAGS -n32 -LANG:std"
				DYNAROPTS="-n32 ${DYNAROPTS}"
				CPPFLAGS_O="${CPPFLAGS_O} -O2 -OPT:Olimit_opt=on -multigot -G 5"
				CPPFLAGS_D="${CPPFLAGS_D} -fullwarn -multigot -G 5 -DEBUG:woff=1375,3201,1424,1110,1209"
				CPPFLAGS_DI="${CPPFLAGS_DI} -g"
				LDFLAGS="$LDFLAGS -n32 -LANG:std"
				AC_DEFINE(IRIX32,)
				ADDITIONAL_COMMENTS="${ADDITIONAL_COMMENTS}Please remember to use LD_LIBRARYN32_PATH instead of\n LD_LIBRARY_PATH - you are using new style 32 bit objects."
			fi

			AC_DEFINE(MIPS,)	
			AC_DEFINE(IRIX,)
		fi

		dnl
		dnl  old SunOS 4.x - only for completeness
		dnl
		if test $OS = SunOS ; then
			CPPFLAGS_D="$CPPFLAGS_D}"
			CPPFLAGS_DI="$CPPFLAGS_DI} -g"
			CPPFLAGS_O="${CPPFLAGS_O} -O2"
			AC_DEFINE(SPARC,)
			AC_DEFINE(SUNOS,)
			AC_ERROR(SunOS not supported - please use Solaris)
		fi

		dnl
		dnl  SUN Solaris (2.x)
		dnl
		if test $OS = Solaris ; then
			dnl
			dnl		Make sure we use at least Workshop 6.0
			dnl		(SUNPro < 6 is a mess - hasn't even heard of ANSI C++!)
			dnl
			if test "${CXX_VERSION_1}" -lt 6 ; then
				AC_MSG_RESULT()
				AC_MSG_RESULT(BALL requires an ANSI C++ compliant compiler)
				AC_MSG_RESULT(SUNPro compilers are ANSI compliant for version 6.0 and above)
				AC_MSG_RESULT(Please upgrade your compiler!)
				AC_MSG_RESULT()
				AC_ERROR(Abort)
			fi

			AC_DEFINE(SOLARIS,)

			TEMPLATE_DIR="SunWS_cache"

			dnl	 set the default binary format (if none selected)
			dnl
			if test "${BINFMT_64_BIT}" = true ; then
				SUN_BINFMT=V9
				CXX_NAME="${CXX_NAME}_V9"
				LDFLAGS="${LDFLAGS} -xarch=v9"
				CPPFLAGS="${CPPLAGS} -xarch=v9"
				AROPTS="${AROPTS} -xarch=v9"
				DYNAROPTS="${DYNAROPTS} -xarch=v9"
			else
				SUN_BINFMT=V8
				CXX_NAME="${CXX_NAME}_V8"
			fi

			DEF_BOOL=true
			AR="${CXX}"
			DYNAR="${CXX}"
			AROPTS="${AROPTS} -xar -KPIC -pto -o"
			DYNAROPTS="${DYNAROPTS} -G -KPIC -pto -o"
			CXX_MAKEDEPEND="${CXX}"
			MAKEDEP_CXX_OPTS="-xM1"
			MAKEDEP_CXX_SUFFIX=" >.Dependencies"

			AC_DEFINE(BALL_NO_INLINE_FUNCTIONS,)
			CPPFLAGS="${CPPFLAGS} -pto -KPIC"
			CPPFLAGS_O="${CPPFLAGS_O} -xO3 -xtarget=native"
			CPPFLAGS_D="${CPPFLAGS_D}"
			CPPFLAGS_DI="${CPPFLAGS_DI} -g"
		fi
	fi
fi

dnl
dnl   checking for DEBUG-Flag
dnl

AC_MSG_CHECKING(for DEBUG flag)
if test "$DEBUG" != "" ; then
	dnl   define a debug flag and prevent the compilation of
	dnl   inline functions by defining BALL_NO_INLINE_FUNCTIONS
	dnl   (see COMMON/debug.h)
	if test "$DEBUG" = true ; then	
		dnl  if debug information is also required, add the corresponding flag
		dnl
		if test "${DEBUG_INFO}" = true -a "$CPPFLAGS_DI" != "" ; then
			CPPFLAGS_D="${CPPFLAGS_D} ${CPPFLAGS_DI}"
		fi
		AC_DEFINE(BALL_DEBUG,)
		AC_DEFINE(BALL_NO_INLINE_FUNCTIONS,)
		AC_MSG_RESULT(enabled)
		CPP_MODE_FLAGS="${CPPFLAGS_D}"
		CPP_MODE_FLAGS_NO_OPTIMIZATION="${CPPFLAGS_D}"
	else
		AC_MSG_RESULT(disabled)
		CPP_MODE_FLAGS="${CPPFLAGS_O}"
		CPP_MODE_FLAGS_NO_OPTIMIZATION=""
	fi
else
	AC_MSG_RESULT(disabled)
	CPP_MODE_FLAGS="${CPPFLAGS_O}"
	CPP_MODE_FLAGS_NO_OPTIMIZATION=""
fi

dnl
dnl		check for multi-platform build
dnl
if test "${MULTI_BUILD}" = "true" ; then
	AC_MSG_CHECKING(multi-platform build)
	AC_MSG_RESULT(enabled)
fi

dnl
dnl		checks for header files
dnl

dnl
dnl		some platforms need to include ieeefp.h
dnl		for the definition of finite
dnl
if test "$OS" = IRIX -o "$OS" = Solaris ; then
	INCLUDE_IEEEFP=true
	AC_DEFINE(BALL_INCLUDE_IEEEFP,)
fi

dnl 	check for standard C headers
dnl 	and how to run the preprocessor
AC_HEADER_STDC

dnl		check for limits header (class numeric limits, to be precise)
dnl
dnl
AC_MSG_CHECKING(for numeric_limits class)
cat > /tmp/$$.conftest.c << EOF
#include <limits>
int main()
{
	float f = std::numeric_limits<float>::min();
	return 0;
}
EOF
if ${CXX} /tmp/$$.conftest.c 2>/dev/null >/dev/null; then
	AC_MSG_RESULT(available)
	AC_DEFINE(HAVE_NUMERIC_LIMITS)
else
	AC_MSG_RESULT(not available)
fi
rm /tmp/$$.conftest.c a.out 2>/dev/null

dnl
dnl		check for template arguments needed for friends of template
dnl 	classses. Some compilers require "<>" after the method name,
dnl		others don't - so let's find it out!
dnl
AC_MSG_CHECKING(for null template arguments)
BALL_NULL_TEMPLATE_ARGS="NULL"
AC_TRY_COMPILE(
	[
		template <typename T>
		class A
		{
			public:
			friend bool operator == <> (const A&, const A&);
		};
	],
	[	
	],
	BALL_NULL_TEMPLATE_ARGS="<>")	
if test "${BALL_NULL_TEMPLATE_ARGS}" = "NULL" ; then
	AC_TRY_COMPILE(
		[
			template <typename T>
			class A
			{
				public:
				friend bool operator == (const A&, const A&);
			};
		],
		[
		],
		BALL_NULL_TEMPLATE_ARGS="")	
fi
AC_MSG_RESULT(\"$BALL_NULL_TEMPLATE_ARGS\")
if test "${BALL_NULL_TEMPLATE_ARGS}" = "NULL" ; then
	AC_MSG_RESULT(could not find a suitable argument for null templates)
	AC_ERROR(aborted)
fi

dnl
dnl		check for ANSI compliant <iostream>
dnl 	We need this for the base classes (ios vs. basic_ios<char>) in socket.h/C
dnl
AC_MSG_CHECKING(for ANSI compliant iostream)
BALL_HAS_ANSI_IOSTREAM=no
AC_TRY_COMPILE(
	[
		#include <iostream>
		class A:public std::iostream
		{
			A():std::basic_ios<char>(0),std::iostream(0)
			{}
		};
	],
	[	
 	],
	BALL_HAS_ANSI_IOSTREAM=yes
)	
AC_MSG_RESULT($BALL_HAS_ANSI_IOSTREAM)

dnl
dnl		check for ANSI or ARM style access modification
dnl		either (ARM style) Base::foo or (ANSI style) using Base::foo 	
dnl
AC_MSG_CHECKING(for ANSI or ARM style access modification)
BALL_CFG_USING_METHOD_DIRECTIVE=none
AC_TRY_COMPILE(
	[
		class A
		{
			protected: void foo(){};
		};

		class B : public A
		{
			public: using A::foo;
		};
	],
	[	
		B b;
		b.foo();
 	],
	BALL_CFG_USING_METHOD_DIRECTIVE=ANSI
)	
if test ${BALL_CFG_USING_METHOD_DIRECTIVE} = none ; then
	AC_TRY_COMPILE(
		[
			class A
			{
				protected: void foo(){};
			};

			class B : public A
			{
				public: A::foo;
			};
		],
		[	
			B b;
			b.foo();
		],
		BALL_CFG_USING_METHOD_DIRECTIVE=ARM
	)	
fi
AC_MSG_RESULT(${BALL_CFG_USING_METHOD_DIRECTIVE})
if test ${BALL_CFG_USING_METHOD_DIRECTIVE} = ANSI ; then
	AC_DEFINE(BALL_CFG_USING_METHOD_DIRECTIVE)
fi
if test ${BALL_CFG_USING_METHOD_DIRECTIVE} = none ; then
	AC_MSG_RESULT()
	AC_MSG_RESULT([Compiler does not understand ARM or ANSI style method access modification.])
	AC_MSG_RESULT([Please specify a different compiler (e.g. g++ 2.95.2) using the option])
	AC_MSG_RESULT([--with-compiler=<compiler>.])
	AC_MSG_RESULT()
	AC_ERROR(aborted.)
fi


dnl
dnl		Checks for typedefs, structures, and compiler characteristics.
dnl

AC_TYPE_SIZE_T
AC_HEADER_TIME

dnl
dnl		check for the size of int and pointers (may cause trouble on 64 bit architectures)
dnl		we define the type PointerInt (in COMMON/global.h) according to the macro
dnl		BALL_POINTERSIZE_INT (which is set here)
dnl   We also define a 64 bit unsigned numeric type. All pointers that are read or written
dnl   in persistence-related methods use this type to ensure compatibility between 32 and
dnl   64bit BALL versions.
dnl	  missing: usage of the result of AC_TYPE_SIZE_T
dnl
AC_CHECK_SIZEOF(int, 4)
AC_CHECK_SIZEOF(long, 4)
AC_CHECK_SIZEOF(long long, 8)
AC_CHECK_SIZEOF(size_t, 4)
AC_CHECK_SIZEOF(void*, 4)

SIZEOF_INT=$ac_cv_sizeof_int
SIZEOF_LONG=$ac_cv_sizeof_long
SIZEOF_SIZE_T=$ac_cv_sizeof_size_t
SIZEOF_VOID_P=$ac_cv_sizeof_voidp
SIZEOF_UINT=$ac_cv_sizeof_int
SIZEOF_ULONG=$ac_cv_sizeof_long
SIZEOF_ULONGLONG=$ac_cv_sizeof_long_long

AC_DEFINE_UNQUOTED(BALL_INT_SIZE,"${SIZEOF_INT}")
AC_DEFINE_UNQUOTED(BALL_LONG_SIZE,"${SIZEOF_LONG}")
AC_DEFINE_UNQUOTED(BALL_SIZE_T_SIZE,"${SIZEOF_SIZE_T}")
AC_DEFINE_UNQUOTED(BALL_POINTER_SIZE,"${SIZEOF_VOID_P}")
AC_DEFINE_UNQUOTED(BALL_UINT_SIZE,"${SIZEOF_UINT}")
AC_DEFINE_UNQUOTED(BALL_ULONG_SIZE,"${SIZEOF_ULONG}")
AC_DEFINE_UNQUOTED(BALL_ULONGLONG_SIZE,"${SIZEOF_ULONGLONG}")

if test "${SIZEOF_VOID_P}" = "${SIZEOF_UINT}" ; then
	BALL_POINTERSIZE_INT="unsigned int"
else 
	if test "${SIZEOF_VOID_P}" = "${SIZEOF_ULONG}" ; then
		BALL_POINTERSIZE_INT="unsigned long"
	else
		AC_MSG_RESULT()
		AC_MSG_RESULT(cannot find appropriate numeric type of same size as void*)
		AC_MSG_ERROR(abort)
	fi
fi
AC_DEFINE_UNQUOTED(BALL_POINTERSIZE_INT, ${BALL_POINTERSIZE_INT})
if test "${SIZEOF_SIZE_T}" = "${SIZEOF_INT}" ; then
	BALL_INDEX_TYPE="int"
	BALL_SIZE_TYPE="unsigned int"
else 
	if test "${SIZEOF_SIZE_T}" = "${SIZEOF_LONG}" ; then
		BALL_INDEX_TYPE="long"
		BALL_SIZE_TYPE="unsigned long"
	else
		AC_MSG_RESULT()
		AC_MSG_RESULT(cannot find appropriate numeric type of same size as size_t)
		AC_MSG_ERROR(abort)
	fi
fi
AC_DEFINE_UNQUOTED(BALL_SIZE_TYPE, ${BALL_SIZE_TYPE})
AC_DEFINE_UNQUOTED(BALL_INDEX_TYPE, ${BALL_INDEX_TYPE})

dnl  define 64 bit unsigned type
if test "${SIZEOF_ULONG}" = "8" ; then
	BALL_ULONG64_TYPE="unsigned long"
else 
	if test "${SIZEOF_ULONGLONG}" = "8" ; then
		BALL_ULONG64_TYPE="unsigned long long"
	else
		AC_MSG_RESULT()
		AC_MSG_RESULT(cannot find unsigned 64bit type.)
		AC_MSG_RESULT()
		AC_MSG_ERROR(abort)
	fi
fi
AC_DEFINE_UNQUOTED(BALL_ULONG64_TYPE, ${BALL_ULONG64_TYPE})
	

dnl   
dnl   check for some network stuff needed for socket class
dnl   this test tries to identify the neccessary libraries
dnl		solaris needs xnet or socket/nsl to link the tcp stuff
dnl  	

dnl
dnl 	first check if everythings already defined in libc
dnl 
AC_CHECK_FUNCS(inet_addr, HAVE_INET_ADDR=1)
AC_CHECK_FUNCS(gethostbyname, HAVE_GETHOSTBYNAME=1)
if test "${HAVE_INET_ADDR+set}" = set ; then
	AC_CHECK_FUNC(inet_aton, HAVE_INET_ATON=1)
fi 

dnl   if gethostbyname was not defined in libc, try libxnet (Solaris only?)
if test "${HAVE_GETHOSTBYNAME+set}" != set -a "${USE_LIBXNET}" != false; then
	AC_CHECK_LIB(xnet, gethostbyname)
	unset ac_cv_func_gethostbyname
	AC_CHECK_FUNCS(gethostbyname,HAVE_GETHOSTBYNAME=1)
fi
if test "${HAVE_INET_ADDR+set}" != set ; then
	unset ac_cv_func_inet_addr
	AC_CHECK_FUNCS(inet_addr,HAVE_INET_ADDR=1)
	if test "${HAVE_INET_ADDR+set}" != set -a "${USE_LIBXNET}" != false; then
		AC_CHECK_LIB(xnet, inet_addr)
		unset ac_cv_func_inet_addr
		AC_CHECK_FUNCS(inet_addr,HAVE_INET_ADDR=1)
	fi
fi

if test "${HAVE_GETHOSTBYNAME+set}" != set ; then 
	AC_CHECK_LIB(nsl, gethostbyname)
	unset ac_cv_func_gethostbyname
	AC_CHECK_FUNCS(gethostbyname,HAVE_GETHOSTBYNAME=1)
fi

if test "${HAVE_INET_ADDR+set}" != set ; then
	AC_CHECK_LIB(socket, inet_addr)
	unset ac_cv_func_inet_addr
	AC_CHECK_FUNCS(inet_addr,HAVE_INET_ADDR=1)
fi


dnl check again whether inet_aton exists (perhaps it was hidden in one
dnl of the other libraries..
if test "${HAVE_INET_ATON+set}" != set ; then
	unset ac_cv_func_inet_aton
	AC_CHECK_FUNC(inet_aton,HAVE_INET_ATON=1)
fi

if test "${HAVE_INET_ATON+set}" = set ; then
	AC_DEFINE(HAVE_INET_ATON,)
fi

dnl
dnl		Now for something related: check whether size arguments
dnl 	in function calls like getsockname getpeername or accept
dnl		require a specialized typename or int
dnl		We simply compile a short example with all known types
dnl		and take one that didn't cause a warning (or an error)
dnl
AC_MSG_CHECKING(for socketlen type)
AC_TRY_COMPILE(
	[
#include <sys/socket.h>	
#include <netinet/in.h>
	],
	[	sockaddr_in   addr;
	  socklen_t     len = 0;
	  getsockname(0, (struct sockaddr*)&addr, &len);
	],
	BALL_SOCKLEN_TYPE=socklen_t)	
if test "${BALL_SOCKLEN_TYPE}" = "" ; then
	AC_TRY_COMPILE(
		[
#include <sys/socket.h>	
#include <netinet/in.h>
		],
		[	sockaddr_in   addr;
		  size_t    len = 0;
		  getsockname(0, (struct sockaddr*)&addr, &len);
		],
		BALL_SOCKLEN_TYPE=size_t)	
fi
if test "${BALL_SOCKLEN_TYPE}" = "" ; then
	AC_TRY_COMPILE(
		[
#include <sys/socket.h>	
#include <netinet/in.h>
		],
		[	sockaddr_in   addr;
		  unsigned int  len = 0;
		  getsockname(0, (struct sockaddr*)&addr, &len);
		],
		BALL_SOCKLEN_TYPE="unsigned int")	
fi
if test "${BALL_SOCKLEN_TYPE}" = "" ; then
	AC_TRY_COMPILE(
		[
#include <sys/socket.h>	
#include <netinet/in.h>
		],
		[	sockaddr_in   addr;
		  int 					len = 0;
		  getsockname(0, (struct sockaddr*)&addr, &len);
		],
		BALL_SOCKLEN_TYPE="int")	
fi
if test "${BALL_SOCKLEN_TYPE}" = "" ; then
	AC_MSG_RESULT(FAILED)
	AC_MSG_RESULT(-------------WARNING!---------------)
	AC_MSG_RESULT(could not find a matching type for socket length argument)
	AC_MSG_RESULT(in call to getsockname)
	AC_MSG_RESULT(please check the setting for BALL_SOCKLEN_TYPE in config.mak)
	AC_MSG_RESULT(and set it to the type needed for the third arguemnt of  getsockname)
	AC_MSG_RESULT()
else
	AC_MSG_RESULT($BALL_SOCKLEN_TYPE)
fi

AC_DEFINE_UNQUOTED(BALL_SOCKLEN_TYPE, ${BALL_SOCKLEN_TYPE})


dnl
dnl   check whether ieeefp.h does really exist
dnl
if test "$INCLUDE_IEEEFP" = true ; then
	AC_CHECK_HEADERS(ieeefp.h)
	AC_DEFINE(INCLUDE_IEEEFP,)
fi

dnl
dnl		Checks for library functions
dnl

dnl
dnl		BALLVIEW
dnl

dnl
dnl    search for X-libs and includes and BALLView (OpenGL/MESA) stuff
dnl 
if test "${USE_BALLVIEW}" = true ; then
	AC_PATH_X
	X11_INCPATH=${x_includes}
	X11_LIBPATH=${x_libraries}

	if test "${no_x}" = "yes" ; then
		USE_BALLVIEW=false
	fi

	if test "${USE_BALLVIEW}" = true ; then
		if test "${X11_LIBPATH}" = "/usr/lib" -o "${X11_LIBPATH}" = "" ; then
			X11_LIBPATH=""
			X11_LIBPATHOPT=""
		else
			X11_LIBPATHOPT="-L${X11_LIBPATH}"
		fi
	fi


	if test "${USE_BALLVIEW}" = true ; then
		AC_MSG_CHECKING(for OpenGL/Mesa)
		AC_MSG_RESULT($BALLVIEW_PLATFORM)

		if test "${BALLVIEW_PLATFORM}" = Mesa ; then
			AC_MSG_CHECKING(for Mesa includes)
			AC_FIND_HEADER(MESA_INCLUDES,GL/gl.h, ${OPENGL_INCPATH} ${X11_INCPATH})
			if test "${MESA_INCLUDES}" = "" ; then
				AC_MSG_RESULT((not found!))
				AC_MSG_RESULT()
				AC_MSG_RESULT(No Mesa headers found! Please specify the path to the directory)
				AC_MSG_RESULT(containing the Mesa headers using --with-opengl-incl=DIR.)
				AC_MSG_RESULT(Mesa can be obtained from www.mesa3d.org.)
				AC_MSG_RESULT(Disabling support for visualization component BALLVIEW)
				USE_BALLVIEW=false
			else
				AC_MSG_RESULT(${MESA_INCLUDES})
			fi

			if test "${USE_BALLVIEW}" = true ; then
				AC_MSG_CHECKING(for Mesa library)
				AC_FIND_LIB(MESA_LIBS,libMesaGL, ${OPENGL_LIBPATH} ${X11_LIBPATH})
				if test "${MESA_LIBS}" = "" ; then
					AC_FIND_LIB(MESA_LIBS,libGL, ${OPENGL_LIBPATH} ${X11_LIBPATH})
				fi
				if test "${MESA_LIBS}" = "" ; then
					AC_MSG_RESULT((not found!))
					AC_MSG_RESULT()
					AC_MSG_RESULT(No Mesa library libMesaGL or libGL found! Please specify the path)
					AC_MSG_RESULT(to the directory containing the library using the --with-opengl-libs=DIR.)
					AC_MSG_RESULT(Mesa can be obtained from www.mesa3d.org.)
					AC_MSG_RESULT(Disabling support for visualization component BALLVIEW)
					USE_BALLVIEW=false
				else
					AC_MSG_RESULT((${MESA_LIBS}))
				fi
			fi
			
			dnl prevent the use of -L/usr/lib - this may lead to problems with different
			dnl binary formats (e.g. SGI O32/N32 format)
			if test "${MESA_INCLUDES}" != /usr/include && test "${MESA_INCLUDES}" != "" ; then
				BALLVIEW_INCLUDES="${BALLVIEW_INCLUDES} -I${MESA_INCLUDES}"
			fi
		fi

		if test ${BALLVIEW_PLATFORM} = OpenGL ; then
			AC_MSG_CHECKING(for OpenGL includes)
			AC_FIND_HEADER(OPENGL_INCPATH,GL/gl.h)
			if test "${OPENGL_INCPATH}" = "" ; then
				AC_MSG_RESULT((not found!))
				AC_MSG_RESULT()
				AC_MSG_RESULT(no OpenGL headers found! Please modify the top section)
				AC_MSG_RESULT(of configure and specify the correct path to these headers.)
				AC_MSG_ERROR(aborted)
			else
				AC_MSG_RESULT((${OPENGL_INCPATH}))
			fi

			AC_MSG_CHECKING(for OpenGL library)
			AC_FIND_LIB(OPENGL_LIBPATH,libGL)
			if test "${OPENGL_LIBPATH}" = "" ; then
				AC_MSG_RESULT((not found!))
				AC_MSG_RESULT()
				AC_MSG_RESULT(no OpenGL lib found! Please modify the top section)
				AC_MSG_RESULT(of configure and specify the correct path to these libraries.)
				AC_MSG_ERROR(aborted)
			else
				AC_MSG_RESULT((${OPENGL_LIBPATH}))
			fi
			
			if test "${OPENGL_INCPATH}" != /usr/include && test "${OPENGL_INCPATH}" != "" ; then
				BALLVIEW_INCLUDES="${BALLVIEW_INCLUDES} -I${OPENGL_INCPATH}"
			fi
		fi

		AC_MSG_CHECKING(for QT headers)
		if test "${QTDIR}" != "" ; then
			AC_FIND_HEADER(QT_INCPATH,qgl.h,${QTDIR}/include ${BALL_PATH}/contrib/qt/include)
		else
			AC_FIND_HEADER(QT_INCPATH,qgl.h,${BALL_PATH}/contrib/qt/include)
		fi

		if test "${QT_INCPATH}" = "" ; then
			AC_MSG_RESULT((not found!))
			AC_MSG_RESULT()
			AC_MSG_RESULT(No QT header files found! Please specify the path to the QT headers)
			AC_MSG_RESULT(by passing the option --with-qt-incl=DIR to configure.)
			AC_MSG_RESULT(You may also set the environment variable QTDIR to the correct)
			AC_MSG_RESULT(path - configure will recognize this, too.)
			AC_MSG_RESULT(The QT package can be found under the following URL:)
			AC_MSG_RESULT(  http://www.troll.no/qt)
			AC_MSG_ERROR(Building of the visualization component BALLVIEW was disabled!)
		else
			AC_MSG_RESULT((${QT_INCPATH}))	
		fi

		AC_MSG_CHECKING(for QT library)
		if test "${QTDIR}" != "" ; then
			AC_FIND_LIB(QT_LIBPATH,libqt,${QTDIR}/lib ${QTDIR}/lib/${BINFMT} ${BALL_PATH}/contrib/qt/include)
		else
			AC_FIND_LIB(QT_LIBPATH,libqt,${BALL_PATH}/contrib/qt/lib ${BALL_PATH}/contrib/qt/lib/${BINFMT})
		fi

		if test "${QT_LIBPATH}" = "" ; then
			AC_MSG_RESULT((not found!))
			AC_MSG_RESULT()
			AC_MSG_RESULT(The QT library could not be found. Please specify the path to libqt)
			AC_MSG_RESULT(by passing the option --with-qt-libs=DIR to configure.)
			AC_MSG_RESULT(You may also set the environment variable QTDIR to the correct)
			AC_MSG_RESULT(path - configure will recognize this, too.)
			AC_MSG_RESULT(The QT package can be found under the following URL:)
			AC_MSG_RESULT(  http://www.troll.no/qt)
			AC_MSG_ERROR(Building of the visualization component BALLVIEW was disabled!)
		else
			AC_MSG_RESULT((${QT_LIBPATH}))	
		fi

		if test "${QT_INCPATH}" != /usr/include && test "${QT_INCPATH}" != "" ; then
			BALLVIEW_INCLUDES="${BALLVIEW_INCLUDES} -I${QT_INCPATH}"
		fi	
	fi
fi


dnl
dnl   verify libraries needed for BALLVIEW
dnl   (X, QT, Mesa/OpenGL)
dnl

if test "${USE_BALLVIEW}" = true ; then		
	dnl  
	dnl
	dnl  identify the X11 libraries needed to link agains
	dnl
	dnl
	
	AC_MSG_CHECKING(linking against X11 libraries)
	dnl 
	dnl   if the user specified X libraries, try these first
	dnl
	if test "${X11_LIBS}" != "" ; then
		SAVE_LIBS=${LIBS}
		LIBS="${X11_LIBPATHOPT} ${X11_LIBS} ${LIBS}"
		AC_TRY_LINK([],[],X_LINKING_OK=1)
		LIBS=${SAVE_LIBS}
	fi

	dnl 		
	dnl  now try the default guess: Xmu, Xt, and X11 
	dnl
	if test "${X_LINKING_OK+set}" != set ; then
		X11_LIBS="-lXmu -lXt -lX11"
		SAVE_LIBS=${LIBS}
		LIBS="${X11_LIBPATHOPT} ${X11_LIBS} ${LIBS}"
		AC_TRY_LINK([],[],X_LINKING_OK=1)
		LIBS=${SAVE_LIBS}
	fi
	
	dnl 		
	dnl  second guess: add SM and ICE
	dnl
	if test "${X_LINKING_OK+set}" != set ; then
		X11_LIBS="-lXmu -lXt -lX11 -lSM -lICE"
		SAVE_LIBS=${LIBS}
		LIBS="${X11_LIBPATHOPT} ${X11_LIBS} ${LIBS}"
		AC_TRY_LINK([],[],X_LINKING_OK=1)
		LIBS=${SAVE_LIBS}
	fi
	
	dnl 		
	dnl  third guess: Xmu, Xext, Xt, and X11 
	dnl
	if test "${X_LINKING_OK+set}" != set ; then
		X11_LIBS="-lXmu -lXext -lXt -lX11"
		SAVE_LIBS=${LIBS}
		LIBS="${X11_LIBPATHOPT} ${X11_LIBS} ${LIBS}"
		AC_TRY_LINK([],[],X_LINKING_OK=1)
		LIBS=${SAVE_LIBS}
	fi
	
	dnl 		
	dnl fourth guess: add SM and ICE
	dnl
	if test "${X_LINKING_OK+set}" != set ; then
		X11_LIBS="-lXmu -lXext -lXt -lX11 -lSM -lICE"
		SAVE_LIBS=${LIBS}
		LIBS="${X11_LIBPATHOPT} ${X11_LIBS} ${LIBS}"
		AC_TRY_LINK([],[],X_LINKING_OK=1)
		LIBS=${SAVE_LIBS}
	fi
	
	dnl 
	dnl  if we could not link - complain about it!
	dnl
	if test "${X_LINKING_OK+set}" = set ; then
		AC_MSG_RESULT(yes)	
	else
		AC_MSG_RESULT(no)
		AC_MSG_RESULT()
		AC_MSG_RESULT(Don't know how to link with X11 libraries.)
		AC_MSG_RESULT(Please specify the correct libraries (e.g. -lXmu -lXt -lX11) in the)
		AC_MSG_RESULT(environment variable X11_LIBS)
		AC_MSG_RESULT(If you are running Solaris 2.x you might also try the option --without-libxnet)
		AC_MSG_RESULT(if your X libraries were linked against libsocket and libnsl instead of libxnet.)
		AC_MSG_RESULT(Built of visualization component BALLVIEW disabled.)
		AC_MSG_RESULT()
		USE_BALLVIEW=false
	fi

	dnl		
	dnl  define some variables: X11_LIBOPTS and BALLVIEW_LIBS
	dnl
	X11_LIBOPTS="${X11_LIBPATHOPT} ${X11_LIBS}"
fi

if test "${USE_BALLVIEW}" = true ; then
	if test "${BALLVIEW_PLATFORM}" = Mesa ; then
		dnl
		dnl  strip default path
		dnl
		if test "${MESA_LIBS}" != "/usr/lib" -a "${MESA_LIBS}" != "" ; then
			OPENGL_LIBPATH="${MESA_LIBS}"
			OPENGL_LIBPATHOPT="-L${MESA_LIBS}"			
		else
			OPENGL_LIBPATH=""
			OPENGL_LIBPATHOPT=""
		fi
		
		dnl
		dnl  out first guess for the names of the Mesa libraries
		dnl
		OPENGL_LIBS="-lMesaGLU -lMesaGL"

		dnl
		dnl  try to link against mesa libraries
		dnl
		AC_MSG_CHECKING(linking against Mesa libs)
		SAVE_LIBS=${LIBS}
		LIBS="${OPENGL_LIBPATHOPT} ${OPENGL_LIBS} ${X11_LIBOPTS} ${LIBS} "
		AC_TRY_LINK([],[], HAVE_MESALIBS=1)
		LIBS=${SAVE_LIBS}

		dnl
		dnl  could not link agains libMesaGLU/libMesaGL,
    dnl  so try libGLU/libGL
		dnl
		if test "${HAVE_MESALIBS+set}" != set ; then
			OPENGL_LIBS="-lGLU -lGL"
			SAVE_LIBS=${LIBS}
			LIBS="${OPENGL_LIBPATHOPT} ${OPENGL_LIBS} ${X11_LIBOPTS} ${LIBS} "
			AC_TRY_LINK([],[], HAVE_MESALIBS=1)
			LIBS=${SAVE_LIBS}
		fi

		if test "${HAVE_MESALIBS+set}" != set ; then
			AC_MSG_RESULT(no)
			AC_MSG_RESULT()
			AC_MSG_RESULT(Cannot link against libMesaGL/GLU - disabling visualization support!)
			AC_MSG_RESULT(Please specify the path to libMesaGL using --with-opengl-libs=DIR)
			AC_MSG_RESULT()
			USE_BALLVIEW=false
		else
			AC_MSG_RESULT(yes)
			OPENGL_LIBOPTS="${OPENGL_LIBPATHOPT} ${OPENGL_LIBS}"
		fi
	fi
fi
if test "${USE_BALLVIEW}" = true ; then
	if test "${BALLVIEW_PLATFORM}" = OpenGL ; then
		if test "${OPENGL_LIBPATH}" != "/usr/lib" -a "${OPENGL_LIBPATH}" != "" ; then
			OPENGL_LIBOPTS="-L${OPENGL_LIBPATH} -lGLU -lGL"
		else
			OPENGL_LIBPATH=""
			OPENGL_LIBOPTS="-lGLU -lGL"
		fi
		AC_MSG_CHECKING(linking against OpenGL libraries)
		SAVE_LIBS=${LIBS}
		LIBS="${OPENGL_LIBOPTS} ${LIBS}"
		AC_TRY_LINK([],[],OPENGL_LINKING_OK=1)
		LIBS=${SAVE_LIBS}
		if test "${OPENGL_LINKING_OK+set}" != set ; then
			AC_MSG_RESULT(no)
			AC_MSG_RESULT()
			AC_MSG_RESULT(Cannot link against libGL/GLU - disabling visualization support!)
			AC_MSG_RESULT(Please specify the path to OpenGL libraries using --with-opengl-libs=DIR)
			AC_MSG_RESULT()
			USE_BALLVIEW=false
		else
			AC_MSG_RESULT(yes)
		fi
	fi
fi
	
if test "${USE_BALLVIEW}" = true ; then
	if test "${QT_LIBPATH}" != "/usr/lib" ; then
		QT_LIBOPTS="-L${QT_LIBPATH} -lqgl -lqt"
	else 
		QT_LIBPATH=""
		QT_LIBOPTS="-lqgl -lqt"
	fi
fi
if test "${USE_BALLVIEW}" = true ; then
	AC_MSG_CHECKING(linking against QT libraries)
	SAVE_LIBS=${LIBS}
	LIBS="${QT_LIBOPTS} ${OPENGL_LIBOPTS} ${X11_LIBOPTS} ${LIBS}"
	AC_TRY_LINK([const char* qGLVersion();], [qGLVersion();], QT_LINKING_OK=1)
	LIBS=${SAVE_LIBS}
	if test "${QT_LINKING_OK+set}" != set ; then
		AC_MSG_RESULT(no)
		AC_MSG_RESULT()
		AC_MSG_RESULT(Cannot link agains libqgl/qt - disabling support for visualization!)
		AC_MSG_RESULT(If qt is installed, please specify the path to the library)
		AC_MSG_RESULT(using the option --with-qt-libs=DIR)
		AC_MSG_RESULT()
		USE_BALLVIEW=false
	else
		AC_MSG_RESULT(yes)
		
		dnl  
		dnl  identify the version of the library
		dnl
		AC_MSG_CHECKING(QT library version)
		SAVE_LIBS=${LIBS}
		LIBS="${QT_LIBOPTS} ${OPENGL_LIBOPTS} ${X11_LIBOPTS} ${LIBS}"
		LD_LIBRARY_PATH=${LD_LIBRARY_PATH}:${X11_LIBPATH}:${OPENGL_LIBPATH}:${QT_LIBPATH}
		export LD_LIBRARY_PATH
		echo "LD_LIBRARY_PATH = ${LD_LIBRARY_PATH}" 1>&5
		AC_TRY_RUN(
			[
				#include <stdio.h> 
				const char* qVersion();
				int main()
				{
					FILE* f = fopen("qt.version", "w");
					fprintf(f, "%s\n", qVersion());
					fclose(f);
					exit(0);
				}
			], 
			QT_VERSION_OK=1,
			DUMMY=0,
			DUMMY=0
		)
		LIBS=${SAVE_LIBS}
		
		dnl
		dnl	if the program compiled and ran successfully,
		dnl extract the QT version number
		dnl
		if test "${QT_VERSION_OK+set}" != set; then
			AC_MSG_RESULT(no)
			AC_MSG_RESULT()
			AC_MSG_RESULT(The execution of a program linked against the QT)
			AC_MSG_RESULT(library failed. Please have a look at config.log)
			AC_MSG_RESULT((the last few lines) to find out what happened.)
			AC_MSG_RESULT(Perhaps you specified the wrong library or the)
			AC_MSG_RESULT(X11 libraries are in conflict with any other library.)
			AC_MSG_RESULT(You might also want to check your LD_LIBRARY_PATH.)
			AC_MSG_RESULT(Support for visualization is disabled.)
			AC_MSG_RESULT()
			USE_BALLVIEW=false
		else
			QT_VERSION_STRING=`cat qt.version`
			AC_MSG_RESULT(${QT_VERSION_STRING})

			dnl
			dnl  test whether this version is the right one
			dnl  (2.x.y and at lease 2.0.2
			dnl
			rm qt.version 2>/dev/null
			QT_MAJOR=`echo ${QT_VERSION_STRING} | ${CUT} -d. -f1`
			if test "${QT_MAJOR}" != 2 ; then
				AC_MSG_RESULT()
				AC_MSG_RESULT(QT version 2.x is required.)
				AC_MSG_RESULT(Please install version QT Version 2 (at least 2.0.2))
				AC_MSG_RESULT(which can be obtained from)
				AC_MSG_RESULT()
				AC_MSG_RESULT(  www.troll.no/qt)
				AC_MSG_RESULT()
				AC_MSG_RESULT(Support for visualization is disabled.)
				AC_MSG_RESULT()
				USE_BALLVIEW=false
			else
				dnl
				dnl  give a hint if somebody is using 2.00 which
				dnl  has a serious bug in QListView: multiple selection
        dnl  does not work as expected!
				dnl
				if test "${QT_VERSION_STRING}" = "2.00" ; then
					AC_MSG_RESULT()
					AC_MSG_RESULT(QT verison 2.00 is known to contain a bug)
					AC_MSG_RESULT(that affects some features of MOLVIEW.)
					AC_MSG_RESULT(Please install at least version 2.0.2)
					AC_MSG_RESULT(which can be obtained from)
					AC_MSG_RESULT()
					AC_MSG_RESULT(  www.troll.no/qt)
					AC_MSG_RESULT()
					AC_MSG_RESULT()
				fi
			fi
		fi
	fi
fi

if test "${USE_BALLVIEW}" = "true" ; then
	LIBBALLVIEW="libVIEW.a libMOLVIEW.a"
	BALLVIEW="VIEW MOLVIEW"
else
	BALLVIEW=
fi

BALL_INCLUDES="-I$BALL_PATH/include"

dnl
dnl		expand BINFMT to include the compiler name
dnl
BINFMT="${BINFMT}-${CXX_NAME}"


dnl		MULTI-BUILD MODE ONLY:
dnl 	add the binary format to the list of supported binary formats
dnl 	held in config/binary_formats. Avoid double entries
dnl
if test "${MULTI_BUILD}" = "true" ; then
	touch ${BINFORMAT_FILE}
	if test "`${GREP} \^${BINFMT}\\$ ${BINFORMAT_FILE}`" = "" ; then
		echo ${BINFMT} >> ${BINFORMAT_FILE}
	fi
fi

dnl
dnl   create the global config.h (the one including the platform specific
dnl   config.h.${BINFMT})
dnl
if test "${MULTI_BUILD}" = "true" ; then
	${CAT} config/config.h.header | ${SED} 1,2d > config.h
	LINES=`cat config/binary_formats | wc -l`
	i=1
	while test $i -le $LINES ; do
		BFMT=`cat ${BINFORMAT_FILE} | ${SED} -n ${i}p`
		echo "#if ( BFMT == $i )" >> config.h
		echo "#	include <BALL/CONFIG/config.h.${BFMT}>" >> config.h
		echo "#endif" >> config.h
		echo " " >> config.h
		i=`expr $i + 1`
	done
	${CAT} config/config.h.footer | ${SED} 1,2d >> config.h
	if test -f ${BALL_PATH}/include/BALL/CONFIG/config.h ; then
		if test "`${DIFF} ${BALL_PATH}/include/BALL/CONFIG/config.h config.h`" != "" ; then
			${RM} ${BALL_PATH}/include/BALL/CONFIG/config.h
			${MV} config.h  ${BALL_PATH}/include/BALL/CONFIG/config.h
		else 
			${RM} config.h
		fi
	else
		${MV} config.h  ${BALL_PATH}/include/BALL/CONFIG/config.h
	fi

	BINFMT_INDEX="-DBFMT="`${GREP} -n ${BINFMT} ${BINFORMAT_FILE} | ${CUT} -d: -f1 | ${TAIL} -1`
else
	BINFMT_INDEX=""
fi

LIBS="${LIBS} -lm"
BALLVIEW_LIBS="${BALLVIEW_LIBS} ${QT_LIBOPTS} ${OPENGL_LIBOPTS} ${X11_LIBOPTS} -lm"

BALL_LIBS="${BALL_LIBS} -L${BALL_PATH}/lib/${BINFMT} -lBALL"
if test "${USE_BALLVIEW}" = true ; then
	BALLVIEW_INCLUDES="${BALL_INCLUDES} ${BALLVIEW_INCLUDES}"
	BALLVIEW_LIBS="-L${BALL_PATH}/lib/${BINFMT} -lMOLVIEW -lVIEW -lBALL ${BALLVIEW_LIBS}"
fi

LDFLAGS="$LDFLAGS"
LIBS="$BALL_LIBS $LIBS"


dnl
dnl   joining all flags
dnl
CPPFLAGS="$CPPFLAGS $DEFINES"

dnl
dnl  we try to remove dependencies from non-BALL files
dnl  to speed up compilation
dnl  This works only for compilers that create one-line dependencies
dnl  (most compilers except for g++). We simply "grep" away all foreign stuff.
dnl  We can only insert ou egrep filter, if the compiler needs
dnl  a redirection of the output (MAKEDEP_CXX_SUFFIX != "")
dnl
dnl  btw: escaping is FUN! the expression below uses \ to escape the damned shell,
dnl  [] to escape m4, and finally $$ to escape make - the correct expression
dnl  is something like "(${BALL_PATH}|^[^/]*$)". Send me an email if got that immediately...
dnl
if test "${HAS_GPLUSPLUS}" = false -a "${MAKEDEP_CXX_SUFFIX}" != "" ; then
	MAKEDEP_CXX_SUFFIX="|egrep \"(${BALL_PATH}|^[[^/]]*\$\$)\" ${MAKEDEP_CXX_SUFFIX}"
fi



dnl  
dnl   neccessary variable substitutions in *.mak
dnl


dnl         PATHS

AC_SUBST(BALL_PATH)         dnl path to top level directory
AC_SUBST(BALL_INCLUDES)     dnl includes 


dnl					DEPENDENCIES

MAKEDEP_CXX_OPTS="${MAKEDEP_CXX_OPTS} ${CPPFLAGS}"
if test ${CXX_MAKEDEPEND} = "makedepend" ; then
	MAKEDEP_CXX_OPTS="${MAKEDEP_CXX_OPTS} -- "
fi
	
AC_SUBST(CXX_MAKEDEPEND)		dnl path to C++ makedepend
AC_SUBST(MAKEDEP_CXX_OPTS)	dnl options to C++ makedepend
AC_SUBST(MAKEDEP_CXX_SUFFIX)dnl optional suffix (for redirection) for C++ makedepend
AC_SUBST(DEPENDENCY_GREPS)  dnl command line to remove non-BALL headers from the dependencies


dnl         COMPILER

AC_SUBST(CXX)               dnl C++ compiler (absolute path)
AC_SUBST(CPPFLAGS)          dnl C++ compiler flags
AC_SUBST(ADD_CPPFLAGS)      dnl additional C++ compiler falgs (not passed on to makedepend)
AC_SUBST(BALL_CPPFLAGS)			dnl C++ compiler flags used during compilation of the library only
AC_SUBST(HAS_GPLUSPLUS)     dnl true, if C++ compiler is g++
AC_SUBST(STD_CPP_INCLUDES)  dnl standard include paths of C++ compiler
AC_SUBST(MAKEDEP_OPTS)      dnl options for makedep (needed for Linux)
AC_SUBST(BALL_TYPENAME)			dnl if the compiler requires the use of typename
                            dnl this variable is set to "typename". "" otherwise.
AC_SUBST(BINFMT_INDEX)      dnl BFMT: binformat index of the current BINFMT - passed to the compiler
                            dnl   as -DBFMT=${BINFMT_INDEX} to decide which config.h to use
                            dnl   used in multi-platform build only!

dnl         LINKER

AC_SUBST(BALLVIEW_LIBS)     dnl BALLVIEW libs
AC_SUBST(OPENGL_LIBOPTS)    dnl compiler options for OpenGL libs (-Lxxx -lGLU -lGL)
AC_SUBST(OPENGL_LIBPATH)    dnl path to OpenGL libs (without -L)
AC_SUBST(QT_LIBOPTS)        dnl copiler options for QT libs (-Lxxx -lqgl -lqt)
AC_SUBST(QT_LIBPATH)        dnl path to QT libs (without -L)
AC_SUBST(X11_LIBOPTS)       dnl compiler options for X11 libs (-Lxx -lXmu -lXt -lX11 [-lSM -lICE])
AC_SUBST(X11_LIBPATH)       dnl path to X11 libs (without -L)
AC_SUBST(X11_LIBS)          dnl the X11 libraries only (-lXmu -lXt...)
AC_SUBST(BALL_LIBS)         dnl libraries
AC_SUBST(LDFLAGS)           dnl linker flags
AC_SUBST(LIBS)              dnl linker libraries
AC_SUBST(DEFINES)           dnl defines (part of LDFLAGS)
AC_SUBST(CPPFLAGS_D)        dnl debug flags for compiler 
AC_SUBST(CPPFLAGS_O)        dnl optimization flags for compiler 
AC_SUBST(CPP_MODE_FLAGS)    dnl set to CPPFLAGS_D|CPPFLAGS_O, depending on BALL_DEBUG
AC_SUBST(CPP_MODE_FLAGS_NO_OPTIMIZATION)    
														dnl set to CPPFLAGS_D|"", depending on BALL_DEBUG





dnl					ARCHIVER

dnl if nothing has been defined for AR/AROPTS,
dnl use default

if test "${AR}" = "no" ; then
	AR="ar"
fi
if test "${AROPTS}" = "" ; then
	AROPTS="cru"
fi

AC_SUBST(AR)
AC_SUBST(AROPTS)
AC_SUBST(DYNAR)
AC_SUBST(DYNAROPTS)


dnl				  MISC

dnl   ADDTIONAL_COMMENTS is substituted at the end of the
dnl   information text in Makefile and will be printed after
dnl 	the library has been built.
dnl   Any hints concerning a special OS/compiler combination 
dnl   might want to print some information here (as is the
dnl   case for the IRIX LD_LBRARY_PATH/LD_LIBRARY64_PATH/LD_LIBRARYN32_PATH)
dnl   If BALLVIEW is to be built, we add a hint for MolVIEW.
if test "${ADDITIONAL_COMMENTS}" = "" ; then
	ADDITIONAL_COMMENTS=""
fi
if test "${USE_BALLVIEW}" = true ; then
	ADDITIONAL_COMMENTS="${ADDITIONAL_COMMENTS}\n\nTo build the molecule viewer MolVIEW, please type\n\n   make molview\n\nThe executable is built in the directory APPLICATIONS/MOLVIEW\n"
fi
AC_SUBST(ADDITIONAL_COMMENTS)

dnl	    BALLVIEW

AC_SUBST(USE_BALLVIEW)				dnl bool: use ballview (default=true)
AC_SUBST(BALLVIEW)						dnl if USE_BALLVIEW=true, BALLVIEW. "" otherwise. Used in config.mak
AC_SUBST(BALLVIEW_PLATFORM)		dnl Mesa or OpenGL
AC_SUBST(BALLVIEW_INCLUDES)		dnl BALLVIEW includes
AC_SUBST(LIBBALLVIEW)					dnl static library name


dnl    	define the standard installation path
AC_DEFINE_UNQUOTED(BALL_PATH, "${BALL_PATH}")


dnl			define BALL_NULL_TEMPLATE_ARGS
AC_DEFINE_UNQUOTED(BALL_NULL_TEMPLATE_ARGS, ${BALL_NULL_TEMPLATE_ARGS})

dnl			define BALL_HAS_ANSI_IOSTREAM
if test "$BALL_HAS_ANSI_IOSTREAM" = yes ; then
	AC_DEFINE(BALL_HAS_ANSI_IOSTREAM)
fi

dnl	
dnl		 make target "clean" should also remove 
dnl    template repositories. The name of this directory 
dnl    depends on the compiler and ist set in TEMPLATE_DIR
dnl
AC_SUBST(TEMPLATE_DIR)

dnl
dnl    create files
dnl

SUBDIRS="COMMON CONCEPT DATATYPE ENERGY FORMAT KERNEL MATHS MOLMEC NMR SOLVATION STRUCTURE SYSTEM ${BALLVIEW}"
AC_SUBST(SUBDIRS)

if test "${MULTI_BUILD}" = "true" ; then
	AC_MSG_RESULT(creating shadow directories...)
	config/shadowsource.sh `pwd`"/${BINFMT}" `pwd` "${SUBDIRS} TEST EXAMPLES TUTORIAL APPLICATIONS"
	${RM} -fr `pwd`/${BINFMT}/TEST/data 2>/dev/null
	${LN} -s `pwd`/TEST/data `pwd`/${BINFMT}/TEST 2>/dev/null
	${LN} -s `pwd`/TEST/runtests `pwd`/${BINFMT}/TEST 2>/dev/null

	${CP} config/Makefile.multiplatform Makefile

	dnl   define the string to substitute in common.mak
	BINFMT_PATH="/${BINFMT}"
else
	BINFMT_PATH=""
fi

AC_OUTPUT([
	Makefile.tmp:config/Makefile.in
	config.mak.tmp:config/config.mak.in 
	common.mak.tmp:config/common.mak.in 
])

if test "${MULTI_BUILD}" = "true" ; then
	${MV} Makefile.tmp ${BINFMT}/Makefile
	${MV} common.mak.tmp ${BINFMT}/common.mak
	${MV} config.mak.tmp ${BINFMT}/config.mak
else 
	${MV} Makefile.tmp Makefile
	${MV} common.mak.tmp common.mak
	${MV} config.mak.tmp config.mak
fi

dnl
dnl    copy config.h to the includes directory
dnl    remove obsolete dependency file
dnl

if test "${MULTI_BUILD}" = "true" ; then
	${MV} -f config.h $BALL_PATH/include/BALL/CONFIG/config.h.${BINFMT}
else
	${MV} -f config.h $BALL_PATH/include/BALL/CONFIG/config.h
fi
	

dnl
dnl		make sure the dependencies are (re)built
dnl
if test "${MULTI_BUILD}" = "true" ; then
	${RM}  ${BINFMT}/.Dependencies 2>/dev/null
else
	${RM}  .Dependencies 2>/dev/null
fi

dnl
dnl Just give another helpful message and exit
dnl

AC_MSG_RESULT()
AC_MSG_RESULT()
AC_MSG_RESULT(Your Makefiles have been created. If everything went OK: just type)
AC_MSG_RESULT()
AC_MSG_RESULT(		make)
AC_MSG_RESULT()
AC_MSG_RESULT(to build BALL.)
AC_MSG_RESULT()
